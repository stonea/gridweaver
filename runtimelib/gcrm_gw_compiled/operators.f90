   MODULE operators

   USE kinds
   USE numbers

   USE grid_params
   USE grid_metrics
   USE grid_subdomain

   USE wrap_data
!  USE wrp1D_data

   IMPLICIT NONE
   SAVE

   CONTAINS
!=======================================================================
!  BEGIN jacobian
!=======================================================================
   SUBROUTINE jacobian (km0,km1,x1,x2,x3)
!-----------------------------------------------------------------------
!                       ___
!                   1   \   x1(0) + x1(i)  x2(i+1) - x2(i-1)
!       J(x1,x2) = ---- /   -------------  ----------------- 
!                   A0  ---       2                3
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL jacobian_blk (nsd,area_inv(:,:,nsd), &
                            x1(:,:,k,nsd),x2(:,:,k,nsd),x3(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE jacobian
!=======================================================================
!  END jacobian
!=======================================================================

!=======================================================================
!  BEGIN jacobian_blk
!=======================================================================
   SUBROUTINE jacobian_blk (nsd,area_inv,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      nsd
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),x1(im,jm),x2(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x3(i,j) = sixth*area_inv(i,j)* &
                       ((x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i  ,j-1))+ &
                        (x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(i+1,j+1))+ &
                        (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
      ENDDO
   ENDDO

! NORTH POLE
   i=2;j=jm;
   x3(i,j) = sixth*area_inv(i,j)* &
                       ((x1(i,j)+x1(i+1,j  ))*(x2(im ,  1)-x2(i  ,j-1))+ &
                        (x1(i,j)+x1(im ,  1))*(x2(i-1,j  )-x2(i+1,j  ))+ &
                        (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(im ,  1))+ &
                        (x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
! SOUTH POLE
   i=im;j=2;
   x3(i,j) = sixth*area_inv(i,j)* &
                       ((x1(i,j)+x1(  1, jm))*(x2(i  ,j+1)-x2(i  ,j-1))+ &
                        (x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(  1, jm))+ &
                        (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x1(i,j)+x1(i  ,j-1))*(x2(  1, jm)-x2(i-1,j-1)))

! FIX-UP NORTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_north(nsd)) THEN
      i=2;j=2;
      x3(i,j) = sixth*area_inv(i,j)* &
                       ((x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i  ,j-1))+ &
                        (x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x1(i,j)+x1(i  ,j+1))*(x2(i-1,j-1)-x2(i+1,j+1))+ &
                        (x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i  ,j+1))+ &
                        (x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
   ENDIF

! FIX-UP SOUTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_south(nsd)) THEN
      i=2;j=2;
      x3(i,j) = sixth*area_inv(i,j)* &
                       ((x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i-1,j-1))+ &
                        (x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(i+1,j+1))+ &
                        (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x1(i,j)+x1(i-1,j-1))*(x2(i+1,j  )-x2(i-1,j  )))
   ENDIF

   END SUBROUTINE jacobian_blk
!=======================================================================
!  END jacobian_blk
!=======================================================================

!=======================================================================
!  BEGIN jacobian2
!=======================================================================
   SUBROUTINE jacobian2 (km0,km1,x0,x1,x2,x3)
!-----------------------------------------------------------------------
!                          ___
!                      1   \   x0(0) + x0(i) x1(0) + x1(i)  x2(i+1) - x2(i-1)
!       J(x0,x1,x2) = ---- /   ------------- -------------  ----------------- 
!                      A0  ---       2             2                3
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x0(im,jm,km0:km1,nsdm),x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL jacobian2_blk (nsd,area_inv(:,:,nsd), &
                            x0(:,:,k,nsd),x1(:,:,k,nsd),x2(:,:,k,nsd),x3(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE jacobian2
!=======================================================================
!  END jacobian2
!=======================================================================

!=======================================================================
!  BEGIN jacobian2_blk
!=======================================================================
   SUBROUTINE jacobian2_blk (nsd,area_inv,x0,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      nsd
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),x0(im,jm),x1(im,jm),x2(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x3(i,j) = sixth*area_inv(i,j)* &
                       ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i  ,j-1))+ &
                        (x0(i,j)+x0(i+1,j+1))*(x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(i+1,j+1))+ &
                        (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
      ENDDO
   ENDDO

! NORTH POLE
   i=2;j=jm;
   x3(i,j) = sixth*area_inv(i,j)* &
                       ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(im ,  1)-x2(i  ,j-1))+ &
                        (x0(i,j)+x0(im ,  1))*(x1(i,j)+x1(im ,  1))*(x2(i-1,j  )-x2(i+1,j  ))+ &
                        (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(im ,  1))+ &
                        (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
! SOUTH POLE
   i=im;j=2;
   x3(i,j) = sixth*area_inv(i,j)* &
                       ((x0(i,j)+x0(  1, jm))*(x1(i,j)+x1(  1, jm))*(x2(i  ,j+1)-x2(i  ,j-1))+ &
                        (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(  1, jm))+ &
                        (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i-1,j  ))+ &
                        (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(  1, jm)-x2(i-1,j-1)))

! FIX-UP NORTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_north(nsd)) THEN
      i=2;j=2;
      x3(i,j) = sixth*area_inv(i,j)* &
                       ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i  ,j-1))+ &
                        (x0(i,j)+x0(i+1,j+1))*(x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i-1,j-1)-x2(i+1,j+1))+ &
                        (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i  ,j-1)-x2(i  ,j+1))+ &
                        (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i+1,j  )-x2(i-1,j-1)))
   ENDIF

! FIX-UP SOUTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_south(nsd)) THEN
      i=2;j=2;
      x3(i,j) = sixth*area_inv(i,j)* &
                       ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(i+1,j+1)-x2(i-1,j-1))+ &
                        (x0(i,j)+x0(i+1,j+1))*(x1(i,j)+x1(i+1,j+1))*(x2(i  ,j+1)-x2(i+1,j  ))+ &
                        (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i-1,j  )-x2(i+1,j+1))+ &
                        (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j-1)-x2(i  ,j+1))+ &
                        (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i+1,j  )-x2(i-1,j  )))
   ENDIF

   END SUBROUTINE jacobian2_blk
!=======================================================================
!  END jacobian2_blk
!=======================================================================

!=======================================================================
!  BEGIN jacobian_linear_corner
!=======================================================================
   SUBROUTINE jacobian_linear_corner (wrap_name,km0,km1,x1,x2,x3)
!-----------------------------------------------------------------------
!  jacobian based on linear interpolation to cell corners
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
   CHARACTER (LEN=*) :: &
      wrap_name
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
   REAL (KIND=dbl_kind) :: &
      x2_crn(crnm,im,jm,km0:km1,nsdm),tmpry_edg(edgm,im,jm,km0:km1,nsdm)
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   CALL interpolate_to_corner (km0,km1,x2,x2_crn)

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL jacobian_linear_corner_blk (d_edge(:,:,:,nsd), &
                     x1(:,:,k,nsd),x2_crn(:,:,:,k,nsd),tmpry_edg(:,:,:,k,nsd))
      ENDDO
   ENDDO

   CALL wrap (wrap_name,edge_scalar=tmpry_edg)

   CALL curl_edge (km0,km1,tmpry_edg,x3)

   END SUBROUTINE jacobian_linear_corner
!=======================================================================
!  END jacobian_linear_corner
!=======================================================================

!=======================================================================
!  BEGIN jacobian_linear_corner_blk
!=======================================================================
   SUBROUTINE jacobian_linear_corner_blk (d_edge,x1,x2_crn,tmpry_edg)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      d_edge(6,im,jm),x1(im,jm),x2_crn(crnm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      tmpry_edg(edgm,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   tmpry_edg = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         tmpry_edg(1,i,j) = half*(x1(i,j)+x1(i+1,j  ))*(x2_crn(1,i  ,j  )-x2_crn(2,i  ,j-1))/d_edge(1,i,j)
         tmpry_edg(2,i,j) = half*(x1(i,j)+x1(i+1,j+1))*(x2_crn(2,i  ,j  )-x2_crn(1,i  ,j  ))/d_edge(2,i,j)
         tmpry_edg(3,i,j) = half*(x1(i,j)+x1(i  ,j+1))*(x2_crn(1,i-1,j  )-x2_crn(2,i  ,j  ))/d_edge(3,i,j)
      ENDDO
   ENDDO

   END SUBROUTINE jacobian_linear_corner_blk
!=======================================================================
!  END jacobian_linear_corner_blk
!=======================================================================

!=======================================================================
!  END jacobian_4th
!=======================================================================
   SUBROUTINE jacobian_4th (wrap_name,km0,km1,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   CHARACTER (LEN=*) :: &
      wrap_name
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      n,nn,k,nlst
   REAL (KIND=dbl_kind),ALLOCATABLE :: &
      x1_1D(:,:),x2_1D(:,:),x3_1D(:,:),tmpry1(:,:),tmpry2(:,:)
   REAL (KIND=dbl_kind) :: &
      x1_nghbr(6),x2_nghbr(6),dx2_nghbr(6)
   REAL (KIND=dbl_kind),PARAMETER :: &
      omomo = 1.5_dbl_kind
   TYPE (grid_node),POINTER :: &
      ptr
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:,:,:) = zero


   END SUBROUTINE jacobian_4th
!=======================================================================
!  END jacobian_4th
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence
!=======================================================================
   SUBROUTINE flux_divergence (km0,km1,x1,x2,x3)
!-----------------------------------------------------------------------
!                             ___
!      __       __        1   \   x1(0) + x1(i)  x2(i) - x2(0)
!      \/ ¥ (x1 \/ x2) = ---- /   -------------  -------------  l(i)
!                         A0  ---       2             L(i)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL flux_divergence_blk (area_inv       (  :,:,nsd), &
                                   laplacian_wghts(:,:,:,nsd), &
                                   x1(:,:,k,nsd),x2(:,:,k,nsd),x3(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE flux_divergence
!=======================================================================
!  END flux_divergence
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence_blk
!=======================================================================
   SUBROUTINE flux_divergence_blk (area_inv,laplacian_wghts,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),laplacian_wghts(6,im,jm),x1(im,jm),x2(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x3(i,j) = half*area_inv(i,j)* &
        ((x1(i,j)+x1(i+1,j  ))*(x2(i+1,j  )-x2(i,j))*laplacian_wghts(1,i,j)+ &
         (x1(i,j)+x1(i+1,j+1))*(x2(i+1,j+1)-x2(i,j))*laplacian_wghts(2,i,j)+ &
         (x1(i,j)+x1(i  ,j+1))*(x2(i  ,j+1)-x2(i,j))*laplacian_wghts(3,i,j)+ &
         (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))*laplacian_wghts(4,i,j)+ &
         (x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))*laplacian_wghts(5,i,j)+ &
         (x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j))*laplacian_wghts(6,i,j))
      ENDDO
   ENDDO

! NORTH POLE
   i = 2; j = jm
   x3(i,j) = half*area_inv(i,j)*laplacian_wghts(1,i,j)* &
                               ((x1(i,j)+x1(i+1,j  ))*(x2(i+1,j  )-x2(i,j))+ &
                                (x1(i,j)+x1(im ,  1))*(x2(im ,  1)-x2(i,j))+ &
                                (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))+ &
                                (x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))+ &
                                (x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j)))
! SOUTH POLE
   i = im; j = 2
   x3(i,j) = half*area_inv(i,j)*laplacian_wghts(1,i,j)* &
                               ((x1(i,j)+x1(  1, jm))*(x2(  1, jm)-x2(i,j))+ &
                                (x1(i,j)+x1(i  ,j+1))*(x2(i  ,j+1)-x2(i,j))+ &
                                (x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))+ &
                                (x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))+ &
                                (x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j)))

   END SUBROUTINE flux_divergence_blk
!=======================================================================
!  END flux_divergence_blk
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence2
!=======================================================================
   SUBROUTINE flux_divergence2 (km0,km1,x0,x1,x2,x3)
!-----------------------------------------------------------------------
!                                ___
!      __          __        1   \   x0(0) + x0(i)  x1(0) + x1(i)  x2(i) - x2(0)
!      \/ ¥ (x0 x1 \/ x2) = ---- /   -------------  -------------  -------------  l(i)
!                            A0  ---       2              2            L(i)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x0(im,jm,km0:km1,nsdm),x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL flux_divergence2_blk (area_inv       (  :,:,nsd), &
                                   laplacian_wghts(:,:,:,nsd), &
                                   x0(:,:,k,nsd),x1(:,:,k,nsd),x2(:,:,k,nsd),x3(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE flux_divergence2
!=======================================================================
!  END flux_divergence2
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence2_blk
!=======================================================================
   SUBROUTINE flux_divergence2_blk (area_inv,laplacian_wghts,x0,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),laplacian_wghts(6,im,jm),x0(im,jm),x1(im,jm),x2(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x3(i,j) = (one/four)*area_inv(i,j)* &
        ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(i+1,j  )-x2(i,j))*laplacian_wghts(1,i,j)+ &
         (x0(i,j)+x0(i+1,j+1))*(x1(i,j)+x1(i+1,j+1))*(x2(i+1,j+1)-x2(i,j))*laplacian_wghts(2,i,j)+ &
         (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i  ,j+1)-x2(i,j))*laplacian_wghts(3,i,j)+ &
         (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))*laplacian_wghts(4,i,j)+ &
         (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))*laplacian_wghts(5,i,j)+ &
         (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j))*laplacian_wghts(6,i,j))
      ENDDO
   ENDDO

! NORTH POLE
   i = 2; j = jm
   x3(i,j) = (one/four)*area_inv(i,j)*laplacian_wghts(1,i,j)* &
                               ((x0(i,j)+x0(i+1,j  ))*(x1(i,j)+x1(i+1,j  ))*(x2(i+1,j  )-x2(i,j))+ &
                                (x0(i,j)+x0(im ,  1))*(x1(i,j)+x1(im ,  1))*(x2(im ,  1)-x2(i,j))+ &
                                (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))+ &
                                (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))+ &
                                (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j)))
! SOUTH POLE
   i = im; j = 2
   x3(i,j) = (one/four)*area_inv(i,j)*laplacian_wghts(1,i,j)* &
                               ((x0(i,j)+x0(  1, jm))*(x1(i,j)+x1(  1, jm))*(x2(  1, jm)-x2(i,j))+ &
                                (x0(i,j)+x0(i  ,j+1))*(x1(i,j)+x1(i  ,j+1))*(x2(i  ,j+1)-x2(i,j))+ &
                                (x0(i,j)+x0(i-1,j  ))*(x1(i,j)+x1(i-1,j  ))*(x2(i-1,j  )-x2(i,j))+ &
                                (x0(i,j)+x0(i-1,j-1))*(x1(i,j)+x1(i-1,j-1))*(x2(i-1,j-1)-x2(i,j))+ &
                                (x0(i,j)+x0(i  ,j-1))*(x1(i,j)+x1(i  ,j-1))*(x2(i  ,j-1)-x2(i,j)))

   END SUBROUTINE flux_divergence2_blk
!=======================================================================
!  END flux_divergence2_blk
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence_crn
!=======================================================================
   SUBROUTINE flux_divergence_crn (km0,km1,x1,x2,x3)
!-----------------------------------------------------------------------
!                             ___
!      __       __        1   \   x1(0) + x1(i)  x2(i) - x2(0)
!      \/ ¥ (x1 \/ x2) = ---- /   -------------  -------------  l(i)
!                         A0  ---       2             L(i)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(crnm,im,jm,km0:km1,nsdm),x2(crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL flux_divergence_blk_crn (area_inv_crn         (:,:,:,nsd), &
                                       laplacian_wghts_crn(:,:,:,:,nsd), &
                                   x1(:,:,:,k,nsd),x2(:,:,:,k,nsd),x3(:,:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE flux_divergence_crn
!=======================================================================
!  END flux_divergence_crn
!=======================================================================

!=======================================================================
!  BEGIN flux_divergence_blk_crn
!=======================================================================
   SUBROUTINE flux_divergence_blk_crn (area_inv_crn,laplacian_wghts_crn,x1,x2,x3)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv_crn(crnm,im,jm),laplacian_wghts_crn(3,crnm,im,jm),x1(crnm,im,jm),x2(crnm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(crnm,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x3(:,:,:) = zero
   do j = 2,jm-1
     do i = 2, im-1
        x3(1,i,j) = half*area_inv(1,i,j) * (  &
           laplacian_wghts_crn(1,1,i,j)*(x1(2,i,j-1)+x1(1,i,j))*(x2(2,i,j-1)-x2(1,i,j))+&
           laplacian_wghts_crn(2,1,i,j)*(x1(2,i+1,j)+x1(1,i,j))*(x2(2,i+1,j)-x2(1,i,j))+&
           laplacian_wghts_crn(3,1,i,j)*(x1(2,i,j)  +x1(1,i,j))*(x2(2,i,j)  -x2(1,i,j)) )
        x3(2,i,j) = half*area_inv(2,i,j) * (  &
           laplacian_wghts_crn(1,2,i,j)*(x1(1,i,j)  +x1(2,i,j))*(x2(1,i,j)  -x2(2,i,j))+&
           laplacian_wghts_crn(2,2,i,j)*(x1(1,i,j+1)+x1(2,i,j))*(x2(1,i,j+1)-x2(2,i,j))+&
           laplacian_wghts_crn(3,2,i,j)*(x1(1,i-1,j)+x1(2,i,j))*(x2(1,i-1,j)-x2(2,i,j)) )
     enddo
   enddo

   END SUBROUTINE flux_divergence_blk_crn
!=======================================================================
!  END flux_divergence_blk_crn
!=======================================================================

!=======================================================================
!  BEGIN laplacian
!=======================================================================
   SUBROUTINE laplacian (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!                    ___
!       __2      1   \   x1(i) - x1(0)
!       \/ x1 = ---- /   -------------  l(i)
!                A0  ---      L(i)             
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL laplacian_blk (area_inv(:,:,nsd),laplacian_wghts(:,:,:,nsd), &
                             x1(:,:,k,nsd),x2(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE laplacian
!=======================================================================
!  END laplacian
!=======================================================================

!=======================================================================
!  BEGIN laplacian_blk
!=======================================================================
   SUBROUTINE laplacian_blk (area_inv,laplacian_wghts,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),laplacian_wghts(6,im,jm),x1(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(i,j) = area_inv(i,j)* &
                              ((x1(i+1,j  )-x1(i,j))*laplacian_wghts(1,i,j)+ &
                               (x1(i+1,j+1)-x1(i,j))*laplacian_wghts(2,i,j)+ &
                               (x1(i  ,j+1)-x1(i,j))*laplacian_wghts(3,i,j)+ &
                               (x1(i-1,j  )-x1(i,j))*laplacian_wghts(4,i,j)+ &
                               (x1(i-1,j-1)-x1(i,j))*laplacian_wghts(5,i,j)+ &
                               (x1(i  ,j-1)-x1(i,j))*laplacian_wghts(6,i,j))
      ENDDO
   ENDDO

! NORTH POLE
   i = 2; j = jm
   x2(i,j) = area_inv(i,j)*laplacian_wghts(1,i,j)*((x1(i+1,j  )-x1(i,j))+ &
                                                   (x1(im ,  1)-x1(i,j))+ &
                                                   (x1(i-1,j  )-x1(i,j))+ &
                                                   (x1(i-1,j-1)-x1(i,j))+ &
                                                   (x1(i  ,j-1)-x1(i,j)))
! SOUTH POLE
   i = im; j = 2
   x2(i,j) = area_inv(i,j)*laplacian_wghts(1,i,j)*((x1(  1, jm)-x1(i,j))+ &
                                                   (x1(i  ,j+1)-x1(i,j))+ &
                                                   (x1(i-1,j  )-x1(i,j))+ &
                                                   (x1(i-1,j-1)-x1(i,j))+ &
                                                   (x1(i  ,j-1)-x1(i,j)))

   END SUBROUTINE laplacian_blk
!=======================================================================
!  END laplacian_blk
!=======================================================================

!=======================================================================
!  BEGIN grad_dot_grad
!=======================================================================
   SUBROUTINE grad_dot_grad (km0,km1,x1,x2,x3)
!-----------------------------------------------------------------------
!                           ___
!       __     __       1   \    x1(i) - x1(0)  x2(i) - x2(0)
!       \/ x1¥ \/ x2 = ---- /    -------------  -------------  l(i)
!                       A0  ---        2            L(i)             
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind) :: &
      x1(im,jm,km0:km1,nsdm),x2(im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x3(im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
   REAL (KIND=dbl_kind) :: &
      tmpry1(im,jm),tmpry2(im,jm)
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL flux_divergence_blk (area_inv       (  :,:,nsd), &
                                   laplacian_wghts(:,:,:,nsd), &
                                   x1(:,:,k,nsd),x2(:,:,k,nsd),tmpry1(:,:))

         CALL laplacian_blk       (area_inv       (  :,:,nsd), &
                                   laplacian_wghts(:,:,:,nsd), &
                                   x2(:,:,k,nsd),tmpry2(:,:))

         x3(:,:,k,nsd) = tmpry1(:,:) - x1(:,:,k,nsd)*tmpry2(:,:)
      ENDDO
   ENDDO

   END SUBROUTINE grad_dot_grad
!=======================================================================
!  END grad_dot_grad
!=======================================================================

!=======================================================================
!  BEGIN interpolate_to_center
!=======================================================================
   SUBROUTINE interpolate_to_center (km0,km1,x_crn,x)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind),DIMENSION(crnm,im,jm,km0:km1,nsdm) :: &
      x_crn          !
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(     im,jm,km0:km1,nsdm) :: &
      x              !
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL interpolate_to_center_blk (area_kite_crn(:,:,:,:,  nsd), &
                                         area_inv     (    :,:,  nsd), &
                                         x_crn        (  :,:,:,k,nsd), &
                                         x            (    :,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE interpolate_to_center
!=======================================================================
!  END interpolate_to_center
!=======================================================================

!=======================================================================
!  BEGIN interpolate_to_center_blk
!=======================================================================
   SUBROUTINE interpolate_to_center_blk (area_kite_crn,area_inv,x_crn,x)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(3,crnm,im,jm) :: &
      area_kite_crn
   REAL (KIND=dbl_kind),DIMENSION(       im,jm) :: &
      area_inv
   REAL (KIND=dbl_kind),DIMENSION(  crnm,im,jm) :: &
      x_crn
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(       im,jm) :: &
      x
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x(i,j) = area_inv(i,j)*(area_kite_crn(1,1,i  ,j  )*x_crn(1,i  ,j  )+&
                                 area_kite_crn(1,2,i  ,j  )*x_crn(2,i  ,j  )+&
                                 area_kite_crn(2,1,i-1,j  )*x_crn(1,i-1,j  )+&
                                 area_kite_crn(2,2,i-1,j-1)*x_crn(2,i-1,j-1)+&
                                 area_kite_crn(3,1,i-1,j-1)*x_crn(1,i-1,j-1)+&
                                 area_kite_crn(3,2,i  ,j-1)*x_crn(2,i  ,j-1))
      ENDDO
   ENDDO

!  north pole
   i = 2; j = jm;
   x(i,j) = area_inv(i,j)*(area_kite_crn(1,1,i  ,j  )*x_crn(1,i  ,j  ) + &
                           area_kite_crn(2,1,i-1,j  )*x_crn(1,i-1,j  ) + &
                           area_kite_crn(2,2,i-1,j-1)*x_crn(2,i-1,j-1) + &
                           area_kite_crn(3,1,i-1,j-1)*x_crn(1,i-1,j-1) + &
                           area_kite_crn(3,2,i  ,j-1)*x_crn(2,i  ,j-1))

!  south pole
   i = im; j = 2;
   x(i,j) = area_inv(i,j)*(area_kite_crn(1,2,i  ,j  )*x_crn(2,i  ,j  ) + &
                           area_kite_crn(2,1,i-1,j  )*x_crn(1,i-1,j  ) + &
                           area_kite_crn(2,2,i-1,j-1)*x_crn(2,i-1,j-1) + &
                           area_kite_crn(3,1,i-1,j-1)*x_crn(1,i-1,j-1) + &
                           area_kite_crn(3,2,i  ,j-1)*x_crn(2,i  ,j-1))

   END SUBROUTINE interpolate_to_center_blk
!=======================================================================
!  END interpolate_to_center_blk
!=======================================================================

!=======================================================================
!  BEGIN interp_edg_to_fac
!=======================================================================
   SUBROUTINE interp_edg_to_fac (km0,km1,x_edg,x_fac)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind),DIMENSION(edgm,im,jm,km0:km1,nsdm) :: &
      x_edg          ! x defined at cell edges (edg)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(     im,jm,km0:km1,nsdm) :: &
      x_fac          ! x interpolated to cell centers (fac)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL interp_edg_to_fac_blk (nsd,area_edg(:,:,:,  nsd), &
                                         x_edg   (:,:,:,k,nsd), &
                                         x_fac   (  :,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE interp_edg_to_fac
!=======================================================================
!  END interp_edg_to_fac
!=======================================================================

!=======================================================================
!  BEGIN interp_edg_to_fac_blk
!=======================================================================
   SUBROUTINE interp_edg_to_fac_blk (nsd,area_edg,x_edg,x_fac)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      nsd
   REAL (KIND=dbl_kind),DIMENSION(edgm,im,jm) :: &
      area_edg
   REAL (KIND=dbl_kind),DIMENSION(edgm,im,jm) :: &
      x_edg
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(     im,jm) :: &
      x_fac
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
   REAL (KIND=dbl_kind) :: &
      thng1,thng2

!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x_fac = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         thng1 = area_edg(1,i  ,j  )*x_edg(1,i  ,j  ) + &
                 area_edg(2,i  ,j  )*x_edg(2,i  ,j  ) + &
                 area_edg(3,i  ,j  )*x_edg(3,i  ,j  ) + &
                 area_edg(1,i-1,j  )*x_edg(1,i-1,j  ) + &
                 area_edg(2,i-1,j-1)*x_edg(2,i-1,j-1) + &
                 area_edg(3,i  ,j-1)*x_edg(3,i  ,j-1)

         thng2 = area_edg(1,i  ,j  ) + &
                 area_edg(2,i  ,j  ) + &
                 area_edg(3,i  ,j  ) + &
                 area_edg(1,i-1,j  ) + &
                 area_edg(2,i-1,j-1) + &
                 area_edg(3,i  ,j-1)

         x_fac(i,j) = thng1/thng2
      ENDDO
   ENDDO

!  FIX-UP NORTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_north(nsd)) THEN
      i=2;j=2;
      x_fac(i,j) = 0.2_dbl_kind*(x_edg(1,i  ,j  ) + &
                                 x_edg(2,i  ,j  ) + &
                                 x_edg(3,i  ,j  ) + &
                                 x_edg(2,i-1,j-1) + &
                                 x_edg(3,i  ,j-1))
   ENDIF

!  FIX-UP SOUTHERN HEMISPHERE PENTAGON
   IF (l_sbdmn_pntgn_south(nsd)) THEN
      i=2;j=2;
      x_fac(i,j) = 0.2_dbl_kind*(x_edg(1,i  ,j  ) + &
                                 x_edg(2,i  ,j  ) + &
                                 x_edg(3,i  ,j  ) + &
                                 x_edg(1,i-1,j  ) + &
                                 x_edg(2,i-1,j-1))
   ENDIF

!  NORTH POLE
   i = 2; j = jm;
   x_fac(i,j) = 0.2_dbl_kind*(x_edg(1,i  ,j  ) + &
                              x_edg(2, im,  1) + &
                              x_edg(1,i-1,j  ) + &
                              x_edg(2,i-1,j-1) + &
                              x_edg(3,i  ,j-1))
!  SOUTH POLE
   i = im; j = 2;
   x_fac(i,j) = 0.2_dbl_kind*(x_edg(2,  1, jm) + &
                              x_edg(3,i  ,j  ) + &
                              x_edg(1,i-1,j  ) + &
                              x_edg(2,i-1,j-1) + &
                              x_edg(3,i  ,j-1))

   END SUBROUTINE interp_edg_to_fac_blk
!=======================================================================
!  END interp_edg_to_fac_blk
!=======================================================================

!=======================================================================
!  BEGIN interpolate_to_corner
!=======================================================================
   SUBROUTINE interpolate_to_corner (km0,km1,x,x_crn,l_division_safe)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind),DIMENSION(     im,jm,km0:km1,nsdm) :: &
      x              !
   LOGICAL (KIND=log_kind),OPTIONAL :: &
      l_division_safe
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(crnm,im,jm,km0:km1,nsdm) :: &
      x_crn          !
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL interpolate_to_corner_blk (wghts_crn(:,:,:,:,  nsd), &
                                         x        (    :,:,k,nsd), &
                                         x_crn    (  :,:,:,k,nsd))
      ENDDO
   ENDDO

   IF (PRESENT (l_division_safe)) THEN
      IF (l_division_safe) THEN
         DO nsd = 1,nsdm
            DO k = km0,km1
               WHERE (.NOT.l_msk_crn(:,:,:,nsd)) x_crn(:,:,:,k,nsd) = one
            ENDDO
         ENDDO
      ENDIF
   ENDIF

   END SUBROUTINE interpolate_to_corner
!=======================================================================
!  END interpolate_to_corner
!=======================================================================

!=======================================================================
!  BEGIN interpolate_to_corner_blk
!=======================================================================
   SUBROUTINE interpolate_to_corner_blk (wghts_crn,x,x_crn)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(3,crnm,im,jm) :: &
      wghts_crn
   REAL (KIND=dbl_kind),DIMENSION(       im,jm) :: &
      x
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(  crnm,im,jm) :: &
      x_crn
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x_crn(:,:,:) = zero

   DO j = 1,jm-1
      DO i = 1,im-1
         x_crn(1,i,j) = wghts_crn(1,1,i,j)*x(i  ,j  ) + &
                        wghts_crn(2,1,i,j)*x(i+1,j  ) + &
                        wghts_crn(3,1,i,j)*x(i+1,j+1)

         x_crn(2,i,j) = wghts_crn(1,2,i,j)*x(i  ,j  ) + &
                        wghts_crn(2,2,i,j)*x(i+1,j+1) + &
                        wghts_crn(3,2,i,j)*x(i  ,j+1)
      ENDDO
   ENDDO

!  north pole
   x_crn(1,1,jm) = wghts_crn(1,1,1,jm)*x( 1,jm) + &
                   wghts_crn(2,1,1,jm)*x( 2,jm) + &
                   wghts_crn(3,1,1,jm)*x(im, 1)
   x_crn(1,2,jm) = wghts_crn(1,1,2,jm)*x( 2,jm) + &
                   wghts_crn(2,1,2,jm)*x( 3,jm) + &
                   wghts_crn(3,1,2,jm)*x(im, 1)

!  south pole
   x_crn(2,im,1) = wghts_crn(1,2,im,1)*x(im, 1) + &
                   wghts_crn(2,2,im,1)*x( 1,jm) + &
                   wghts_crn(3,2,im,1)*x(im, 2)
   x_crn(2,im,2) = wghts_crn(1,2,im,2)*x(im, 2) + &
                   wghts_crn(2,2,im,2)*x( 1,jm) + &
                   wghts_crn(3,2,im,2)*x(im, 3)

   END SUBROUTINE interpolate_to_corner_blk
!=======================================================================
!  END interpolate_to_corner_blk
!=======================================================================

!=======================================================================
!  BEGIN gradient_corner
!=======================================================================
   SUBROUTINE gradient_corner (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(       im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(2,crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL gradient_corner_blk (area_inv_crn  (    :,:,:,nsd), &
                                   vctr_wghts_crn(:,:,:,:,:,nsd), &
                                   x1(:,:,k,nsd),x2(:,:,:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE gradient_corner
!=======================================================================
!  END gradient_corner
!=======================================================================

!=======================================================================
!  BEGIN gradient_corner_blk
!=======================================================================
  SUBROUTINE gradient_corner_blk (area_inv_crn,vctr_wghts_crn,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv_crn(crnm,im,jm),vctr_wghts_crn(3,2,crnm,im,jm),x1(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(2,crnm,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j,n
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:,:,:) = zero

   DO j = 1,jm-1
      DO i = 1,im-1        
         DO n = 1,2
            x2(n,1,i,j) = area_inv_crn(1,i,j)* &
                                     (vctr_wghts_crn(1,n,1,i,j)*x1(i  ,j  )+ &
                                      vctr_wghts_crn(2,n,1,i,j)*x1(i+1,j  )+ &
                                      vctr_wghts_crn(3,n,1,i,j)*x1(i+1,j+1))
            x2(n,2,i,j) = area_inv_crn(2,i,j)* &
                                     (vctr_wghts_crn(1,n,2,i,j)*x1(i  ,j  )+ &
                                      vctr_wghts_crn(2,n,2,i,j)*x1(i+1,j+1)+ &
                                      vctr_wghts_crn(3,n,2,i,j)*x1(i  ,j+1))
         ENDDO
      ENDDO
   ENDDO

!  north pole
   DO n = 1,2
      x2(n,1,1,jm) = area_inv_crn(1,1,jm)* &
                                     (vctr_wghts_crn(1,n,1,1,jm)*x1( 1,jm)+ &
                                      vctr_wghts_crn(2,n,1,1,jm)*x1( 2,jm)+ &
                                      vctr_wghts_crn(3,n,1,1,jm)*x1(im, 1))
      x2(n,1,2,jm) = area_inv_crn(1,2,jm)* &
                                     (vctr_wghts_crn(1,n,1,2,jm)*x1( 2,jm)+ &
                                      vctr_wghts_crn(2,n,1,2,jm)*x1( 3,jm)+ &
                                      vctr_wghts_crn(3,n,1,2,jm)*x1(im, 1))
   ENDDO

!  south pole
   DO n = 1,2
      x2(n,2,im,1) = area_inv_crn(2,im,1)* &
                                     (vctr_wghts_crn(1,n,2,im,1)*x1(im, 1)+ &
                                      vctr_wghts_crn(2,n,2,im,1)*x1( 1,jm)+ &
                                      vctr_wghts_crn(3,n,2,im,1)*x1(im, 2))
      x2(n,2,im,2) = area_inv_crn(2,im,2)* &
                                     (vctr_wghts_crn(1,n,2,im,2)*x1(im, 2)+ &
                                      vctr_wghts_crn(2,n,2,im,2)*x1( 1,jm)+ &
                                      vctr_wghts_crn(3,n,2,im,2)*x1(im, 3))
   ENDDO

   END SUBROUTINE gradient_corner_blk
!=======================================================================
!  END gradient_corner_blk
!=======================================================================


!=======================================================================
!  BEGIN gradient_corner_crn
!=======================================================================
   SUBROUTINE gradient_corner_crn (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(2,crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL gradient_corner_crn_blk (area_inv_crn  (    :,:,:,nsd), &
                                   vctr_wghts_crn2(:,:,:,:,:,nsd), &
                                   x1(:,:,:,k,nsd),x2(:,:,:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE gradient_corner_crn
!=======================================================================
!  END gradient_corner_crn
!=======================================================================

!=======================================================================
!  BEGIN gradient_corner_crn_blk
!=======================================================================
  SUBROUTINE gradient_corner_crn_blk (area_inv_crn,vctr_wghts_crn2,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv_crn(crnm,im,jm),vctr_wghts_crn2(3,2,crnm,im,jm),x1(crnm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(2,crnm,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j,n
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:,:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1        
         DO n = 1,2
            x2(n,1,i,j) = area_inv_crn(1,i,j)* &
                                 (vctr_wghts_crn2(1,n,1,i,j)*x1(2,i  ,j-1)+ &
                                  vctr_wghts_crn2(2,n,1,i,j)*x1(2,i+1,j  )+ &
                                  vctr_wghts_crn2(3,n,1,i,j)*x1(2,i  ,j  ))
            x2(n,2,i,j) = area_inv_crn(2,i,j)* &
                                 (vctr_wghts_crn2(1,n,2,i,j)*x1(1,i  ,j  )+ &
                                  vctr_wghts_crn2(2,n,2,i,j)*x1(1,i  ,j+1)+ &
                                  vctr_wghts_crn2(3,n,2,i,j)*x1(1,i-1,j  ))
         ENDDO
      ENDDO
   ENDDO

   END SUBROUTINE gradient_corner_crn_blk
!=======================================================================
!  END gradient_corner_crn_blk
!=======================================================================

!=======================================================================
!  BEGIN gradient_edge
!=======================================================================
   SUBROUTINE gradient_edge (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(     im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(edgm,im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL gradient_edge_blk (d_point_inv(:,:,:,nsd), &
                                 x1(:,:,k,nsd),x2(:,:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE gradient_edge
!=======================================================================
!  END gradient_edge
!=======================================================================

!=======================================================================
!  BEGIN gradient_edge_blk
!=======================================================================
  SUBROUTINE gradient_edge_blk (d_point_inv,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      d_point_inv(6,im,jm),x1(im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(edgm,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:,:) = zero

   DO j = 1,jm-1
      DO i = 1,im-1        
         x2(1,i,j) = d_point_inv(1,i,j)*(x1(i+1,j  )-x1(i,j))
         x2(2,i,j) = d_point_inv(2,i,j)*(x1(i+1,j+1)-x1(i,j))
         x2(3,i,j) = d_point_inv(3,i,j)*(x1(i  ,j+1)-x1(i,j))
      ENDDO
   ENDDO

   END SUBROUTINE gradient_edge_blk
!=======================================================================
!  END gradient_edge_blk
!=======================================================================

!=======================================================================
!  BEGIN curl_of_a_vector
!=======================================================================
   SUBROUTINE curl_of_a_vector (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(2,crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(       im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL curl_of_a_vector_blk (area_inv     (      :,:,nsd), &
                                   vctr_wghts_crn(:,:,:,:,:,nsd), &
                                   x1(:,:,:,:,k,nsd),x2(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE curl_of_a_vector
!=======================================================================
!  END curl_of_a_vector
!=======================================================================

!=======================================================================
!  BEGIN curl_of_a_vector_blk
!=======================================================================
  SUBROUTINE curl_of_a_vector_blk (area_inv,vctr_wghts_crn,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),vctr_wghts_crn(3,2,crnm,im,jm),x1(2,crnm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(i,j) = area_inv(i,j)* &
                            (vctr_wghts_crn(1,2,1,i  ,j  )*x1(1,1,i  ,j  ) - &
                             vctr_wghts_crn(1,1,1,i  ,j  )*x1(2,1,i  ,j  ) + &
                             vctr_wghts_crn(1,2,2,i  ,j  )*x1(1,2,i  ,j  ) - &
                             vctr_wghts_crn(1,1,2,i  ,j  )*x1(2,2,i  ,j  ) + &
                             vctr_wghts_crn(2,2,1,i-1,j  )*x1(1,1,i-1,j  ) - &
                             vctr_wghts_crn(2,1,1,i-1,j  )*x1(2,1,i-1,j  ) + &
                             vctr_wghts_crn(2,2,2,i-1,j-1)*x1(1,2,i-1,j-1) - &
                             vctr_wghts_crn(2,1,2,i-1,j-1)*x1(2,2,i-1,j-1) + &
                             vctr_wghts_crn(3,2,1,i-1,j-1)*x1(1,1,i-1,j-1) - &
                             vctr_wghts_crn(3,1,1,i-1,j-1)*x1(2,1,i-1,j-1) + &
                             vctr_wghts_crn(3,2,2,i  ,j-1)*x1(1,2,i  ,j-1) - &
                             vctr_wghts_crn(3,1,2,i  ,j-1)*x1(2,2,i  ,j-1))
      ENDDO
   ENDDO

!  north pole
   i = 2; j = jm;
   x2(i,j) =  area_inv(i,j)*(vctr_wghts_crn(1,2,1,i  ,j  )*x1(1,1,i  ,j  ) - &
                             vctr_wghts_crn(1,1,1,i  ,j  )*x1(2,1,i  ,j  ) + &
                             vctr_wghts_crn(2,2,1,i-1,j  )*x1(1,1,i-1,j  ) - &
                             vctr_wghts_crn(2,1,1,i-1,j  )*x1(2,1,i-1,j  ) + &
                             vctr_wghts_crn(2,2,2,i-1,j-1)*x1(1,2,i-1,j-1) - &
                             vctr_wghts_crn(2,1,2,i-1,j-1)*x1(2,2,i-1,j-1) + &
                             vctr_wghts_crn(3,2,1,i-1,j-1)*x1(1,1,i-1,j-1) - &
                             vctr_wghts_crn(3,1,1,i-1,j-1)*x1(2,1,i-1,j-1) + &
                             vctr_wghts_crn(3,2,2,i  ,j-1)*x1(1,2,i  ,j-1) - &
                             vctr_wghts_crn(3,1,2,i  ,j-1)*x1(2,2,i  ,j-1))

!  south pole
   i = im; j = 2;
   x2(i,j) =  area_inv(i,j)*(vctr_wghts_crn(1,2,2,i  ,j  )*x1(1,2,i  ,j  ) - &
                             vctr_wghts_crn(1,1,2,i  ,j  )*x1(2,2,i  ,j  ) + &
                             vctr_wghts_crn(2,2,1,i-1,j  )*x1(1,1,i-1,j  ) - &
                             vctr_wghts_crn(2,1,1,i-1,j  )*x1(2,1,i-1,j  ) + &
                             vctr_wghts_crn(2,2,2,i-1,j-1)*x1(1,2,i-1,j-1) - &
                             vctr_wghts_crn(2,1,2,i-1,j-1)*x1(2,2,i-1,j-1) + &
                             vctr_wghts_crn(3,2,1,i-1,j-1)*x1(1,1,i-1,j-1) - &
                             vctr_wghts_crn(3,1,1,i-1,j-1)*x1(2,1,i-1,j-1) + &
                             vctr_wghts_crn(3,2,2,i  ,j-1)*x1(1,2,i  ,j-1) - &
                             vctr_wghts_crn(3,1,2,i  ,j-1)*x1(2,2,i  ,j-1))

   END SUBROUTINE curl_of_a_vector_blk
!=======================================================================
!  END curl_of_a_vector_blk
!=======================================================================

!=======================================================================
!  BEGIN curl_edge
!=======================================================================
   SUBROUTINE curl_edge (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(edgm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(     im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL curl_edge_blk (area_inv(  :,:,nsd), &
                             d_edge  (:,:,:,nsd), &
                             x1(:,:,:,k,nsd),x2(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE curl_edge
!=======================================================================
!  END curl_edge
!=======================================================================

!=======================================================================
!  BEGIN curl_edge_blk
!=======================================================================
  SUBROUTINE curl_edge_blk (area_inv,d_edge,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),d_edge(6,im,jm),x1(edgm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(i,j) = area_inv(i,j)*((x1(1,i  ,j  )*d_edge(1,i,j) + &
                                   x1(2,i  ,j  )*d_edge(2,i,j) + &
                                   x1(3,i  ,j  )*d_edge(3,i,j))- &
                                  (x1(1,i-1,j  )*d_edge(4,i,j) + &
                                   x1(2,i-1,j-1)*d_edge(5,i,j) + &
                                   x1(3,i  ,j-1)*d_edge(6,i,j)))
      ENDDO
   ENDDO

! north pole
   i = 2; j = jm;
   x2(i,j) = -area_inv(i,j)*d_edge(1,i,j)*(x1(1,i  ,j  ) + &
                                           x1(2, im,  1) + &
                                           x1(1,i-1,j  ) + &
                                           x1(2,i-1,j-1) + &
                                           x1(3,i  ,j-1))
! south pole
   i = im; j = 2;
   x2(i,j) = -area_inv(i,j)*d_edge(1,i,j)*(x1(2,  1, jm) + &
                                           x1(3,i  ,j  ) + &
                                           x1(1,i-1,j  ) + &
                                           x1(2,i-1,j-1) + &
                                           x1(3,i  ,j-1))

   END SUBROUTINE curl_edge_blk
!=======================================================================
!  END curl_edge_blk
!=======================================================================

!=======================================================================
!  BEGIN curl3D_edge
!=======================================================================
   SUBROUTINE curl3D_edge (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(3,edgm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(3,     im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL curl3D_edge_blk (area_inv(  :,:,nsd), &
                             d_edge  (:,:,:,nsd), &
                             x1(:,:,:,:,k,nsd),x2(:,:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE curl3D_edge
!=======================================================================
!  END curl3D_edge
!=======================================================================

!=======================================================================
!  BEGIN curl3D_edge_blk
!=======================================================================
  SUBROUTINE curl3D_edge_blk (area_inv,d_edge,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),d_edge(6,im,jm),x1(3,edgm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(3,im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2 = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(:,i,j) = area_inv(i,j)*((x1(:,1,i  ,j  )*d_edge(1,i,j) + &
                                     x1(:,2,i  ,j  )*d_edge(2,i,j) + &
                                     x1(:,3,i  ,j  )*d_edge(3,i,j))- &
                                    (x1(:,1,i-1,j  )*d_edge(4,i,j) + &
                                     x1(:,2,i-1,j-1)*d_edge(5,i,j) + &
                                     x1(:,3,i  ,j-1)*d_edge(6,i,j)))
      ENDDO
   ENDDO

! north pole
   i = 2; j = jm;
   x2(:,i,j) = -area_inv(i,j)*d_edge(1,i,j)*(x1(:,1,i  ,j  ) + &
                                             x1(:,2, im,  1) + &
                                             x1(:,1,i-1,j  ) + &
                                             x1(:,2,i-1,j-1) + &
                                             x1(:,3,i  ,j-1))
! south pole
   i = im; j = 2;
   x2(:,i,j) = -area_inv(i,j)*d_edge(1,i,j)*(x1(:,2,  1, jm) + &
                                             x1(:,3,i  ,j  ) + &
                                             x1(:,1,i-1,j  ) + &
                                             x1(:,2,i-1,j-1) + &
                                             x1(:,3,i  ,j-1))

   END SUBROUTINE curl3D_edge_blk
!=======================================================================
!  END curl3D_edge_blk
!=======================================================================

!=======================================================================
!  BEGIN divergence_edge
!=======================================================================
   SUBROUTINE divergence_edge (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(edgm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(     im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL divergence_edge_blk (area_inv(  :,:,nsd), &
                                   d_edge  (:,:,:,nsd), &
                                   x1(:,:,:,k,nsd),x2(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE divergence_edge
!=======================================================================
!  END divergence_edge
!=======================================================================

!=======================================================================
!  BEGIN divergence_edge_blk
!=======================================================================
  SUBROUTINE divergence_edge_blk (area_inv,d_edge,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),d_edge(6,im,jm),x1(edgm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(i,j) = area_inv(i,j)*((x1(1,i  ,j  )*d_edge(1,i,j) + &
                                   x1(2,i  ,j  )*d_edge(2,i,j) + &
                                   x1(3,i  ,j  )*d_edge(3,i,j))- &
                                  (x1(1,i-1,j  )*d_edge(4,i,j) + &
                                   x1(2,i-1,j-1)*d_edge(5,i,j) + &
                                   x1(3,i  ,j-1)*d_edge(6,i,j)))
      ENDDO
   ENDDO

! north pole
   i = 2; j = jm;
   x2(i,j) = -area_inv(i,j)*d_edge(1,i,j)* (x1(1,i  ,j  ) + &
                                            x1(2, im,  1) + &
                                            x1(1,i-1,j  ) + &
                                            x1(2,i-1,j-1) + &
                                            x1(3,i  ,j-1))
! south pole
   i = im; j = 2;
   x2(i,j) = -area_inv(i,j)*d_edge(1,i,j)*(x1(2,  1, jm) + &
                                           x1(3,i  ,j  ) + &
                                           x1(1,i-1,j  ) + &
                                           x1(2,i-1,j-1) + &
                                           x1(3,i  ,j-1))

   END SUBROUTINE divergence_edge_blk
!=======================================================================
!  END divergence_edge_blk
!=======================================================================

!=======================================================================
!  BEGIN divergence_of_a_vector
!=======================================================================
   SUBROUTINE divergence_of_a_vector (km0,km1,x1,x2)
!-----------------------------------------------------------------------
!
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1
   REAL (KIND=dbl_kind) :: &
      x1(2,crnm,im,jm,km0:km1,nsdm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(       im,jm,km0:km1,nsdm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL divergence_of_a_vector_blk (area_inv      (      :,:,nsd), &
                                          vctr_wghts_crn(:,:,:,:,:,nsd), &
                                          x1(:,:,:,:,k,nsd),x2(:,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE divergence_of_a_vector
!=======================================================================
!  END divergence_of_a_vector
!=======================================================================

!=======================================================================
!  BEGIN divergence_of_a_vector_blk
!=======================================================================
  SUBROUTINE divergence_of_a_vector_blk (area_inv,vctr_wghts_crn,x1,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      area_inv(im,jm),vctr_wghts_crn(3,2,crnm,im,jm),x1(2,crnm,im,jm)
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind) :: &
      x2(im,jm)
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:) = zero

   DO j = 2,jm-1
      DO i = 2,im-1
         x2(i,j) = -area_inv(i,j)* &
                             (vctr_wghts_crn(1,1,1,i  ,j  )*x1(1,1,i  ,j  )+ &
                              vctr_wghts_crn(1,2,1,i  ,j  )*x1(2,1,i  ,j  )+ &
                              vctr_wghts_crn(1,1,2,i  ,j  )*x1(1,2,i  ,j  )+ &
                              vctr_wghts_crn(1,2,2,i  ,j  )*x1(2,2,i  ,j  )+ &
                              vctr_wghts_crn(2,1,1,i-1,j  )*x1(1,1,i-1,j  )+ &
                              vctr_wghts_crn(2,2,1,i-1,j  )*x1(2,1,i-1,j  )+ &
                              vctr_wghts_crn(2,1,2,i-1,j-1)*x1(1,2,i-1,j-1)+ &
                              vctr_wghts_crn(2,2,2,i-1,j-1)*x1(2,2,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,1,i-1,j-1)*x1(1,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,2,1,i-1,j-1)*x1(2,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,2,i  ,j-1)*x1(1,2,i  ,j-1)+ &
                              vctr_wghts_crn(3,2,2,i  ,j-1)*x1(2,2,i  ,j-1))
      ENDDO
   ENDDO

!  north pole
   i = 2; j = jm;
   x2(i,j) = -area_inv(i,j)* (vctr_wghts_crn(1,1,1,i  ,j  )*x1(1,1,i  ,j  )+ &
                              vctr_wghts_crn(1,2,1,i  ,j  )*x1(2,1,i  ,j  )+ &
                              vctr_wghts_crn(2,1,1,i-1,j  )*x1(1,1,i-1,j  )+ &
                              vctr_wghts_crn(2,2,1,i-1,j  )*x1(2,1,i-1,j  )+ &
                              vctr_wghts_crn(2,1,2,i-1,j-1)*x1(1,2,i-1,j-1)+ &
                              vctr_wghts_crn(2,2,2,i-1,j-1)*x1(2,2,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,1,i-1,j-1)*x1(1,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,2,1,i-1,j-1)*x1(2,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,2,i  ,j-1)*x1(1,2,i  ,j-1)+ &
                              vctr_wghts_crn(3,2,2,i  ,j-1)*x1(2,2,i  ,j-1))

!  south pole
   i = im; j = 2;
   x2(i,j) = -area_inv(i,j)* (vctr_wghts_crn(1,1,2,i  ,j  )*x1(1,2,i  ,j  )+ &
                              vctr_wghts_crn(1,2,2,i  ,j  )*x1(2,2,i  ,j  )+ &
                              vctr_wghts_crn(2,1,1,i-1,j  )*x1(1,1,i-1,j  )+ &
                              vctr_wghts_crn(2,2,1,i-1,j  )*x1(2,1,i-1,j  )+ &
                              vctr_wghts_crn(2,1,2,i-1,j-1)*x1(1,2,i-1,j-1)+ &
                              vctr_wghts_crn(2,2,2,i-1,j-1)*x1(2,2,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,1,i-1,j-1)*x1(1,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,2,1,i-1,j-1)*x1(2,1,i-1,j-1)+ &
                              vctr_wghts_crn(3,1,2,i  ,j-1)*x1(1,2,i  ,j-1)+ &
                              vctr_wghts_crn(3,2,2,i  ,j-1)*x1(2,2,i  ,j-1))

   END SUBROUTINE divergence_of_a_vector_blk
!=======================================================================
!  END divergence_of_a_vector_blk
!=======================================================================

!=======================================================================
!  BEGIN v_dot_grad_x
!=======================================================================
   SUBROUTINE v_dot_grad_x (km0,km1,x1,wind_crn,x2)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind),DIMENSION(       im,jm,km0:km1,nsdm) :: &
      x1
   REAL (KIND=dbl_kind),DIMENSION(2,crnm,im,jm,km0:km1,nsdm) :: &
      wind_crn
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(       im,jm,km0:km1,nsdm) :: &
      x2
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      crn,i,j,k,nsd
   REAL (KIND=dbl_kind) :: &
      grad_x1_crn(2,crnm,im,jm),tmpry_crn(crnm,im,jm)
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x2(:,:,:,:) = zero

   DO nsd = 1,nsdm
      DO k = km0,km1
         CALL gradient_corner_blk (area_inv_crn  (    :,:,:,  nsd), &
                                   vctr_wghts_crn(:,:,:,:,:,  nsd), &
                                   x1            (      :,:,k,nsd), &
                                   grad_x1_crn   (  :,:,:,:      ))

         DO j = 1,jm-1 ! dot product
            DO i = 1,im-1
               DO crn = 1,crnm
                  tmpry_crn(crn,i,j) = &
                           wind_crn(1,crn,i,j,k,nsd)*grad_x1_crn(1,crn,i,j) + &
                           wind_crn(2,crn,i,j,k,nsd)*grad_x1_crn(2,crn,i,j)
               ENDDO
            ENDDO
         ENDDO

!  north pole
         tmpry_crn(1,1,jm) = wind_crn(1,1,1,jm,k,nsd)*grad_x1_crn(1,1,1,jm) + &
                             wind_crn(2,1,1,jm,k,nsd)*grad_x1_crn(2,1,1,jm)
         tmpry_crn(1,2,jm) = wind_crn(1,1,2,jm,k,nsd)*grad_x1_crn(1,1,2,jm) + &
                             wind_crn(2,1,2,jm,k,nsd)*grad_x1_crn(2,1,2,jm)

!  south pole
         tmpry_crn(2,im,1) = wind_crn(1,2,im,1,k,nsd)*grad_x1_crn(1,2,im,1) + &
                             wind_crn(2,2,im,1,k,nsd)*grad_x1_crn(2,2,im,1)
         tmpry_crn(2,im,2) = wind_crn(1,2,im,2,k,nsd)*grad_x1_crn(1,2,im,2) + &
                             wind_crn(2,2,im,2,k,nsd)*grad_x1_crn(2,2,im,2)

         CALL interpolate_to_center_blk (wghts_crn(:,:,:,:,  nsd), &
                                         area_inv (    :,:,  nsd), &
                                         tmpry_crn(  :,:,:      ), &
                                         x2       (    :,:,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE v_dot_grad_x
!=======================================================================
!  END v_dot_grad_x
!=======================================================================

!=======================================================================
!  BEGIN del_dot_flx
!=======================================================================
   SUBROUTINE del_dot_flx (km0,km1,flx,x)
!.......................................................................
!  INTENT IN
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      km0,km1        ! extents of verical index                       []
   REAL (KIND=dbl_kind),DIMENSION(6,im,jm,km0:km1,nsdm) :: &
      flx
!.......................................................................
!  INTENT OUT
!.......................................................................
   REAL (KIND=dbl_kind),DIMENSION(       im,jm,km0:km1,nsdm) :: &
      x
!.......................................................................
!  LOCAL
!.......................................................................
   INTEGER (KIND=int_kind) :: &
      i,j,k,nsd
!.:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:. .:.

   x(:,:,:,:) = zero

   DO nsd = 1,nsdm
      DO k = km0,km1
         DO j = 2,jm-1
            DO i = 2,im-1
               x(i,j,k,nsd) = area_inv(i,j,nsd)* &
                                  ((flx(1,i  ,j  ,k,nsd)*d_edge(1,i,j,nsd) + &
                                    flx(2,i  ,j  ,k,nsd)*d_edge(2,i,j,nsd) + &
                                    flx(3,i  ,j  ,k,nsd)*d_edge(3,i,j,nsd))- &
                                   (flx(1,i-1,j  ,k,nsd)*d_edge(4,i,j,nsd) + &
                                    flx(2,i-1,j-1,k,nsd)*d_edge(5,i,j,nsd) + &
                                    flx(3,i  ,j-1,k,nsd)*d_edge(6,i,j,nsd)))
            ENDDO
         ENDDO
! north pole
         i = 2; j = jm;
         x(i,j,k,nsd) = -area_inv(i,j,nsd)*d_edge(1,i,j,nsd)* &
                                  (flx(1,i  ,j  ,k,nsd) + &
                                   flx(2, im,  1,k,nsd) + &
                                   flx(1,i-1,j  ,k,nsd) + &
                                   flx(2,i-1,j-1,k,nsd) + &
                                   flx(3,i  ,j-1,k,nsd))
! south pole
         i = im; j = 2;
         x(i,j,k,nsd) = -area_inv(i,j,nsd)*d_edge(1,i,j,nsd)* &
                                  (flx(2,  1, jm,k,nsd) + &
                                   flx(3,i  ,j  ,k,nsd) + &
                                   flx(1,i-1,j  ,k,nsd) + &
                                   flx(2,i-1,j-1,k,nsd) + &
                                   flx(3,i  ,j-1,k,nsd))
      ENDDO
   ENDDO

   END SUBROUTINE del_dot_flx
!=======================================================================
!  END del_dot_flx
!=======================================================================

   END MODULE operators
!cccccccc1ccccccccc2ccccccccc3ccccccccc4ccccccccc5cccgtgccc6ccccccccc7cc
